exm_sources = [
  'main.c',
  'exm-window.c',
  'exm-application.c',
  'exm-comment-dialog.c',

  # Extension Management
  'local/exm-manager.c',
  'local/exm-extension.c',

  # Web Interface
  'web/exm-search-provider.c',
  'web/exm-image-resolver.c',
  'web/exm-request-handler.c',
  'web/exm-comment-provider.c',
  'web/model/exm-search-result.c',
  'web/model/exm-comment.c'
]

exm_deps = [
  dependency('gtk4'),
  dependency('libadwaita-1'),
  dependency('json-glib-1.0'),
  dependency('libsoup-3.0')
]

blueprints = custom_target('blueprints',
  input: files(
    'gtk/help-overlay.blp',
    'exm-window.blp',
    'exm-comment-dialog.blp'
  ),
  output: '.',
  command: [find_program('blueprint-compiler'), 'batch-compile', '@OUTPUT@', '@CURRENT_SOURCE_DIR@', '@INPUT@'],
)

gnome = import('gnome')

exm_sources += gnome.compile_resources('exm-resources',
  'exm.gresource.xml',
  dependencies: blueprints,
  c_name: 'exm'
)

executable('extension-manager', exm_sources,
  dependencies: exm_deps,
  install: true,
)
